#!/usr/bin/perl

use strict;
use warnings;
use lib './lib';

use English qw(-no_match_vars);
use Getopt::Long;
use Pod::Usage;

use FusionInventory::Agent;
use FusionInventory::Agent::Recipient;
use FusionInventory::Agent::Task::Inventory;

my %setup = (
    confdir => './etc',
    datadir => './share',
    libdir  => './lib',
    vardir  => './var',
);

my $options = {
    config => 'none'
};

GetOptions(
    $options,
    'additional-content=s',
    'collect-timeout=s',
    'no-category=s@',
    'scan-homedirs',
    'tag|t=s',
    'recipient=s',
    'verbose',
    'debug+',
    'help',
    'version',
) or pod2usage(-verbose => 0);

if ($options->{version}) {
  print "Inventory task $FusionInventory::Agent::Task::Inventory::VERSION\n";
  exit 0;
}
pod2usage(-verbose => 0, -exitval => 0) if $options->{help};

my $agent = FusionInventory::Agent->new(%setup);
$agent->init(options => $options);

my $task = FusionInventory::Agent::Task::Inventory->new(
    logger => $agent->{logger},
);

$task->configure(
    datadir            => $agent->{datadir},
    deviceid           => $agent->{deviceid},
    tag                => $agent->{config}->{tag},
    timeout            => $agent->{config}->{'collect-timeout'},
    additional_content => $agent->{config}->{'additional-content'},
    scan_homedirs      => $agent->{config}->{'scan-homedirs'},
    no_category        => $agent->{config}->{'no-category'},
);

my$recipient = FusionInventory::Agent::Recipient->create(
    target  => $options->{recipient},
    verbose => $options->{verbose}
);

eval {
    $task->run(recipient => $recipient);
};
if ($EVAL_ERROR) {
    print STDERR "Execution failure:.\n";
    print STDERR $EVAL_ERROR;
    exit 1;
}

__END__

=head1 NAME

fusioninventory-inventory - Standalone inventory

=head1 SYNOPSIS

B<fusioninventory-inventory>
S<[B<--additional-content> I<file>]> S<[B<--no-category> I<string>]>
S<[B<--scan-homedirs>]> S<[B<--timeout> I<integer>]> S<[B<--tag> I<string>]>
S<[B<--recipient> I<string>]> S<[B<--verbose>]> S<[B<--debug>]>

=head1 OPTIONS

=over

=item B<--tag> I<string>

inventory tag

=item B<--timeout> I<integer>

execution timeout (default: 30s)

=item B<--additional-content> I<file>

additional inventory content file

=item B<--no-category> I<string>

exclude specific inventory section

=item B<--scan-homedirs>

scan use home directories (false)

=item B<--recipient> I<string>

where to send result (default: stdout)

=item B<--verbose>

verbose output (control messages)

=item B<--debug>

debug output (execution traces)

=back

=head1 EXAMPLES

    $> fusioninventory-inventory
    $> fusioninventory-inventory
        --recipient /tmp
    $> fusioninventory-inventory
        --recipient http://glpi.domain.com/plugins/fusioninventory
    $> fusioninventory-inventory --no-category printers --no-category softwares
    $> fusioninventory-inventory --additional-content override.xml

=head1 DESCRIPTION

F<fusioninventory-inventory> allows to run an inventory task without a GLPI
server.

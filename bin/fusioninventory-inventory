#!/usr/bin/perl

use strict;
use warnings;
use lib './lib';

use English qw(-no_match_vars);
use Getopt::Long;
use File::Temp;
use Pod::Usage;

use FusionInventory::Agent;
use FusionInventory::Agent::Recipient::Inventory;
use FusionInventory::Agent::Task::Inventory;

my %setup = (
    confdir => './etc',
    datadir => './share',
    libdir  => './lib',
    vardir  => './var',
);

my $options = {
    config => 'none'
};

GetOptions(
    $options,
    'additional-content=s',
    'collect-timeout=s',
    'html',
    'no-category=s',
    'scan-homedirs',
    'tag|t=s',
    'recipient=s',
    'verbose',
    'debug+',
    'help',
    'version',
) or pod2usage(-verbose => 0);

if ($options->{version}) {
  print "Inventory task $FusionInventory::Agent::Task::Inventory::VERSION\n";
  exit 0;
}
pod2usage(-verbose => 0, -exitval => 0) if $options->{help};

my $agent = FusionInventory::Agent->new(%setup);
$agent->init(options => $options);

my $task = FusionInventory::Agent::Task::Inventory->new(
    logger => $agent->{logger},
);

$task->configure(
    datadir            => $agent->{datadir},
    deviceid           => $agent->{deviceid},
    tag                => $agent->{config}->{tag},
    html               => $agent->{config}->{html},
    timeout            => $agent->{config}->{'collect-timeout'},
    additional_content => $agent->{config}->{'additional-content'},
    scan_homedirs      => $agent->{config}->{'scan-homedirs'},
    no_category        => $agent->{config}->{'no-category'},
);

my $recipient = FusionInventory::Agent::Recipient::Inventory->create(
    target  => $options->{recipient},
    task    => 'inventory',
    verbose => $options->{verbose}
);

eval {
    $task->run(
        user         => $agent->{config}->{user},
        password     => $agent->{config}->{password},
        proxy        => $agent->{config}->{proxy},
        ca_cert_file => $agent->{config}->{'ca-cert-file'},
        ca_cert_dir  => $agent->{config}->{'ca-cert-dir'},
        no_ssl_check => $agent->{config}->{'no-ssl-check'},
        recipient    => $recipient
    );
};
if ($EVAL_ERROR) {
    print STDERR "Execution failure:.\n";
    print STDERR $EVAL_ERROR;
    exit 1;
}

__END__

=head1 NAME

fusioninventory-inventory - Standalone inventory

=head1 SYNOPSIS

fusioninventory-inventory [options]

  Options:
    --scan-homedirs                scan use home directories (false)
    --html                         save the inventory as HTML (false)
    -t --tag tag                   inventory tag
    --collect-timeout time         inventory modules execution timeout (30)
    --additional-content file      additional inventory content file
    --verbose                      verbose output (control messages)
    --debug                        debug output (execution traces)
    -h --help                      print this message and exit
    --version                      print the task version and exit

=head1 DESCRIPTION

F<fusioninventory-inventory> allows to run an inventory task without a GLPI
server.
